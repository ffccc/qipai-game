-- 插入房间
CREATE  PROCEDURE dbo.GSP_GS_InsertGameRoom

	-- 索引变量
	@wGameID INT,								-- 模块标识
	@wKindID INT,								-- 类型标识
	@wNodeID INT,								-- 挂接标识
	@wSortID INT,								-- 排序标识

	-- 积分配置
	@lCellScore INT,							-- 单元积分
	@cbRevenueRatio INT,						-- 税收比例

	-- 限制配置
	@lRestrictScore	BIGINT,						-- 限制积分
	@lMinTableScore	BIGINT,						-- 最低积分
	@lMinEnterScore	BIGINT,						-- 最低积分
	@lMaxEnterScore	BIGINT,						-- 最高积分

	-- 会员限制
	@cbMinEnterMember INT,						-- 最低会员
	@cbMaxEnterMember INT,						-- 最高会员

	-- 房间配置
	@dwServerRule INT,							-- 房间规则
	@dwAttachUserRight INT,						-- 附加权限

	-- 房间属性
	@wMaxPlayer INT,							-- 游戏人数
	@wTableCount INT,							-- 桌子数目
	@wServerPort INT,							-- 服务端口
	@wServerType INT,							-- 服务类型
	@strServerName NVARCHAR(32),				-- 房间名字

	-- 连接信息
	@strDataBaseName NVARCHAR(32),				-- 数据库名
	@strDataBaseAddr NVARCHAR(15),				-- 连接地址

	-- 数据设置
	@strCustomRule NVARCHAR(256),				-- 定制规则
	@strServiceMachine NVARCHAR(32),			-- 服务机器

	-- 输出信息
	@strErrorDescribe NVARCHAR(127) OUTPUT		-- 输出信息

WITH ENCRYPTION AS

-- 设置属性
SET NOCOUNT ON

-- 执行逻辑
BEGIN

	-- 查找模块
	DECLARE @GameID INT
	SELECT @GameID=GameID FROM GameGameItem(NOLOCK) WHERE GameID=@wGameID

	-- 存在判断
	IF @GameID IS NULL
	BEGIN
		SET @strErrorDescribe=N'相应的游戏服务组件信息不存在，房间创建失败！'
		RETURN 1
	END

	-- 获取端口
	IF @wServerPort<>0
	BEGIN
		
		-- 获取端口
		DECLARE @ServerPort INT
		SELECT @ServerPort=ServerPort FROM GameRoomInfo(NOLOCK) WHERE ServerPort=@wServerPort AND ServiceMachine=@strServiceMachine

		-- 端口判断
		IF @ServerPort=@wServerPort
		BEGIN
			SET @strErrorDescribe=N'存在服务端口相同的游戏房间，房间创建失败！'
			RETURN 1
		END

	END

	-- 创建房间
	INSERT INTO GameRoomInfo (ServerName, KindID, NodeID, SortID, GameID, TableCount, ServerPort, ServerType, CellScore,
		RevenueRatio, RestrictScore, MinTableScore, MinEnterScore, MaxEnterScore, MinEnterMember, MaxEnterMember, MaxPlayer,
		ServerRule, AttachUserRight, DataBaseName, DataBaseAddr, ServiceMachine, CustomRule)
	VALUES (@strServerName, @wKindID, @wNodeID, @wSortID, @wGameID, @wTableCount, @wServerPort, @wServerType, @lCellScore,
		@cbRevenueRatio, @lRestrictScore, @lMinTableScore, @lMinEnterScore, @lMaxEnterScore, @cbMinEnterMember, @cbMaxEnterMember,
		@wMaxPlayer, @dwServerRule, @dwAttachUserRight, @strDataBaseName, @strDataBaseAddr, @strServiceMachine, @strCustomRule)

	-- 加载房间
	SELECT GameRoomInfo.*, GameGameItem.GameName, GameGameItem.ServerVersion, GameGameItem.ClientVersion, GameGameItem.ServerDLLName,
		GameGameItem.ClientExeName FROM GameRoomInfo(NOLOCK), GameGameItem(NOLOCK)
	WHERE GameRoomInfo.GameID=GameGameItem.GameID AND ServerID=SCOPE_IDENTITY()

END

RETURN 0

GO